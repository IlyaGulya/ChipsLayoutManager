apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

publishing {
    publications {
        release(MavenPublication) {
            groupId = "com.github.IlyaGulya.ChipsLayoutManager"
            artifactId = "ChipsLayoutManager"
            version = "0.3.7-ilyagulya"

            afterEvaluate {
                from components.release
            }
        }

    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    testBuildType "debug"

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 11
        versionName rootProject.ext.libraryVersion

        testApplicationId "com.beloo.chipslayoutmanager.test"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    }
    buildTypes {

        all {
            buildConfigField("boolean", "isLogEnabled", "false")
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        development {
            buildConfigField("boolean", "isLogEnabled", "true")
        }
    }

    sourceSets {
        androidTest {
            res.srcDirs = ['src/androidTest/res']
        }
    }

}

dependencies {
    testImplementation 'junit:junit:4.13.2'

    implementation 'androidx.appcompat:appcompat:1.5.1'
    api 'androidx.recyclerview:recyclerview:1.2.1'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.annotation:annotation:1.5.0'
    androidTestImplementation 'pl.pragmatists:JUnitParams:1.0.5'
}